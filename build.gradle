buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

def VersionBuild = 0
if (System.getenv("BUILD_NUMBER") != null) {
    def buildver = System.getenv("BUILD_NUMBER")
    VersionBuild = "-${buildver}"
}

version = "${config.minecraft_version}-${config.mod_version}${VersionBuild}"
group = "foxie"
archivesBaseName = "BetterSleeping"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "run"
    replace '@VERSION@', "${config.mod_version}"
    mappings = config.mappings
    replaceIn "reference/Reference.java"
    replace "@VERSION@", "${config.mod_version}"
}

repositories {
    maven {
        name "OpenMods"
        url "http://repo.openmods.info/artifactory/openmods"
    }

    maven {
        name "Foxiehost"
        url "http://maven.foxiehost.eu"
    }
}

dependencies {
    compile(group: 'openblocks', name: 'OpenBlocks-API', version: '1.1')
    compile "foxie:FoxieLib:1.8.9-1.0-14:deobf"
    compile "foxie:Calendar:1.8.9-1.0-3:deobf"
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version': project.version, 'mcversion': project.minecraft.version
            }
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }
jar {
    manifest {
        attributes 'FMLCorePlugin': 'mod.foxie.bettersleeping.asm.BetterSleepingCoreLoader'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from(sourceSets.main.java) {
        include "foxie/bettersleeping/api/"
    }
    classifier = 'api'
}

artifacts {
    archives deobfJar
    archives apiJar
}

build.dependsOn deobfJar, apiJar

// deployment
configurations { deployerJars }
dependencies { deployerJars "org.apache.maven.wagon:wagon-ssh:2.2" }

apply plugin: 'maven'
uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars

            repository(url: "scp://maven.foxiehost.lan/home/maven/public/") {
                authentication(userName: "maven", password: "maven")
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'BetterSleeping2'
                    url 'http://foxiemods.com/BetterSleeping2'
                    scm {
                        url 'https://github.com/CallMeFoxie/BetterSleeping2'
                        connection 'scm:git:git@github.com:CallMeFoxie/BetterSleeping2.git'
                        developerConnection 'scm:git:git@github.com:CallMeFoxie/BetterSleeping2.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/CallMeFoxie/BetterSleeping2/issues'
                    }
                    licenses {
                        license {
                            name 'TBD'
                            url 'https://does-not-exist'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'CallMeFoxie'
                            name 'CallMeFoxie'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}
